plugins {
    id 'idea'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.20'
    id 'org.jetbrains.kotlin.kapt' version '1.3.20'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.20'
    id 'org.springframework.boot' version '2.1.2.RELEASE'
    id 'io.zensoft.versioning' version '1.0.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'io.openfuture.chain'
sourceCompatibility = JavaVersion.VERSION_1_8

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    manifest {
        attributes(
                "Implementation-Version": version,
                'Built-By': System.properties['user.name'],
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Export-Package': "io.openfuture.chain.smartcontract;version=${version}"
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "smartcontract-sdk"
            artifacts = [sourceJar]
        }
    }
}

repositories {
    mavenCentral()
}

bootJar.enabled = false

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    // Byte code
    implementation 'org.ow2.asm:asm:7.0'
    implementation 'org.ow2.asm:asm-commons:7.0'

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

// Jar
jar {
    manifest {
        attributes("Implementation-Version": version)
    }
}
